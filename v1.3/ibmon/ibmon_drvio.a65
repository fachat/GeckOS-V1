/****************************************************************************
   
    OS/A65 Version 1.3.10
    Multitasking Operating System for 6502 Computers

    Copyright (C) 1989-1996 Andre Fachat 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

****************************************************************************/



#define   WD1  $e8a0
#define	  CTRL $e8a8
#define   CIA1 $e8b0

	.(

&&DINIT   .(
          lda #%00100000	; just pwr led on
          sta CIA1+CIA_PRA    ;
          lda #%01100101
          sta CIA1+CIA_DDRA    ;
          cmp CIA1+CIA_DDRA    ;
	  bne xini

	lda #%00000010	; control port, motor B off
	sta CTRL

	; disable TA interrupts
	lda #%00000001
	sta CIA1+CIA_ICR

	; prime TA for 65535 cycles, one shot, stop timer
	lda #%00001000
	sta CIA1+CIA_CRA
	lda #255
	sta CIA1+CIA_TAL
	sta CIA1+CIA_TAH 

/*          lda #0       ; der Controller braucht zulange, um das Register
          sta WD1+WD_TRCK     ; upzudaten
          cmp WD1+WD_TRCK
          bne xini
*/          
          ldx #0
i1        lda #00000011       ; 6ms Step-Zeit
          sta drvst,x
          lda #<-1
          sta tracks,x
          inx
          cpx #ANZDRV
          bcc i1
          sta actdrv
          clc
          rts
xini      sec
          rts
          .)

&&DLOOP   .(
dcnt      =sysmem
-sysmem   +=1

          lda #0
          sta dcnt
          sta divers
dl        jsr setdrv     ; a=drive

	; check motor off delay
          ldx divers
          lda hd,x  
          and #%01000000
          beq dl1	
          inc dcnt       ; motor on, check off-delay
          dec mcnt,x
          bne dl1	 ; off-delay not done
          lda hd,x	 ; switch motor off
          and #%10111111
          sta hd,x
          dex
          bmi dl1a	; drive 0

	; switch off drive 1
	lda CTRL
	ora #%00000010
	sta CTRL
	bne dl1
	; switch off drive 0
dl1a   	lda CIA1+CIA_PRA
	ora #%00000100
	sta CIA1+CIA_PRA   
dl1      
	; check disk change 
          ldx divers
          lda CIA1+CIA_PRA
          and #%10000000
          bne dl2		; disk change
	 
          ;lda hd,x
          ;and #1
          ;bne dl2
          lda hd,x
          ora #1
          and #255-2          ; double-stepping aus
          sta hd,x
          lda #<-1
          sta tracks,x
          jsr dchanged
dl2
	; next drive
          inc divers
          lda divers
          cmp #ANZDRV
          bcc dl
          jsr desel
         
	; switch to low density if both motors are off 
          lda CIA1+CIA_PRA
          and #%00000100      ; motor-bit drv a
	  beq dlx
	  lda CTRL
	  and #%00000010	; motor on drv b
	  beq dlx

          jsr lowdens
dlx       lda dcnt            ; mit z-flag bergeben
          rts
          .)

&&setdstep .(
          lda hd,x
          ora #2
          sta hd,x
          rts
          .)
                    
forcend   .(
          lda WD1+WD_ST
          and #1
          beq fer
          lda #%11010000      ; Force Interrupt
          sta WD1+WD_CMD
          jsr wloop
fer       rts
          .)

&selside  .(
          clc
          and #1
          sta side
          bne side1
          lda CIA1+CIA_PRA
          ora #%00000001
          sta CIA1+CIA_PRA
          rts
side1     lda #%11111110
          and CIA1+CIA_PRA
          sta CIA1+CIA_PRA
          rts
          .)

wloop     .(
          txa
          ldx #13
wl1       dex
          bne wl1
          tax
          rts
          .)

wcmd      .(
          pha
#ifdef NMIDEV
	lda #NMI_OFF
	jsr CTRLNMI
#endif
	sei
          jsr wloop
          pla
          sta WD1+WD_CMD
          lda #1
wcl       bit WD1+WD_ST
          beq wcl
          rts
          .)

&setstep  .(
          ldx actdrv
          and #%11111100
          ora #%00001000      ; ohne Motor-On-Time
          sta divers
          lda drvst,x
          and #3
          ora divers
          php
          ;sei
          jsr wcmd
          ldx #128
wc2       jsr setti
wc1       bit CTRL
          bmi wcok
          jsr fragti
          beq wc1
          dex
          bne wc2
          plp
          lda #E_CMD
          sec
          .byt $2c  ; plp,clc berspringen
wcok      plp
          clc
          ldx WD1+WD_ST
          rts
          .)

setti     lda #%00011001	; force load, start TA
          sta CIA1+CIA_CRA
          rts

fragti    lda CIA1+CIA_ICR
          and #%00000001
          rts

&seldrv   .(
          pha
          ldx actdrv
          bmi sd1
          lda WD1+WD_TRCK
          sta tracks,x
sd1       pla
          sta actdrv

          jsr setdrv     ; a=drv
          jsr setdens
          jsr motoron
          
          ldx actdrv
          lda tracks,x
          bpl sd2
          jsr query
          bcs sde
sd2       sta WD1+WD_TRCK
          tay
          ldx actdrv
          lda hd,x
          and #1
          beq sd3
          tya
          eor #1         ; da track von 0-79 kann das nicht aus bereich raus
          jsr go2track   ; um disk-change zu l”schen
          bcs sder
          ldx actdrv
          lda hd,x
          and #%11111110
          sta hd,x
sd3       clc
sder      rts
sde       sta actdrv
          rts
          .)

&motoron  .(
          lda actdrv
          and #1
          beq drva
          lda CTRL
          and #%11111101      ; Motor on
	  sta CTRL
          jmp drvs

drva      lda CIA1+CIA_PRA
          and #%11111011
	  sta CIA1+CIA_PRA

drvs      ldx actdrv
          lda hd,x
          and #%01000000
          bne mok             ; motor war schon an
          sta mcnt,x          ; motor-aus-z„hler auf null
          lda hd,x
          ora #%01000000
          sta hd,x
#ifdef SHOWD
          lda #"m"
          jsr SEROUT
#endif

&mdelay lda CTRL
	and #%00000010
	beq mdel
	lda CIA1+CIA_PRA
	and #%00000100
	bne mok
mdel          
#ifdef SHOWD
          lda #"M"
          jsr SEROUT
#endif
          ldx #40
ml        jsr setti
ml2       jsr SUSPEND
          jsr fragti
          beq ml2
          dex
          bne ml
mok       rts
          .)
                    
&setdrv   .(
          and #1
          beq drva
	; select drive b
          lda CTRL
          ora #%00000001
          bne drvs
drva	; select drive a
          lda CTRL
          and #%11111110      ; drive select
drvs      sta CTRL
&densok   rts
          .)
          
&setdens  .(
          ldx actdrv
          lda hd,x            ; high-Density-Bit
          lsr                 ; auf Portbit schieben
          lsr
          eor CTRL	      ; vergleichen
          and #%00100000      ; maskieren
          beq densok          ; gleich dann fertig
          
          lda hd,x
          bpl lodens
          lda CTRL
          ora #%00100000
          bne denset
lodens    lda CTRL
          and #%11011111
denset    sta CTRL
          jmp mdelay
          .)
          
&lowdens  lda CTRL
          and #%11011111
          sta CTRL
          rts
&highdens ldx actdrv
          lda hd,x
          bpl lowdens
          lda CTRL
          ora #%00100000
          sta CTRL
#ifdef SHOWD
          lda #"H"
          jsr SEROUT
#endif
          jmp mdelay
          
query     .(
          jsr restore
          bcs qx
          lda #4         ; hd-count
          sta divers

          lda #0
          jsr selside
                    
q         ldx #40        ; bis 5.25" auf Drehzahl nach Density-Wechsel
          lda #ERRCNT*2
          sta errcnt
          .byt $2c
qloop     ldx #16         ; 8*32ms=256ms <> 1 umdrehung bei 234 upm
          lda #%11000000 ; Read Adress
          ldy #5
          sei
          jsr wcmd
q1        jsr setti
          lda #%00000001
q2        bit CIA1+CIA_ICR
          bne qn
          bit CTRL
          bvc q2
          ldx WD1+WD_DATA
          stx data,y
          dey
          bpl q2
          lda WD1+WD_ST
          and #%00001100
          bne q3
          lda data+2     ; Sektor Length
          and #3
          beq q3         ; Sektoren mit 128 Byte gehen nicht
          tax
          lda POT2-1,x   ; dann Anzahl 256-Byte-Blocks im Sektor
          ldx actdrv
          sta blen,x
          cli
#ifdef NMIDEV
	lda #NMI_ON
	jsr CTRLNMI
#endif
          lda data+5
          clc
          rts

qn        lda WD1+WD_ST
          dex
          bne q1
          
q3        dec errcnt
          bne qloop
          cli
#ifdef NMIDEV
	lda #NMI_ON
	jsr CTRLNMI
#endif
          dec divers
          beq qe
          
          ldx actdrv
          lda hd,x
          eor #%10000000
          sta hd,x
          jsr setdens
          
          lda divers
          pha
          cmp #2
          bcs q4
          jsr restore
q4        pla
          sta divers
          jmp q
          
qe        lda #E_Q
qx        sec
          rts
          
          .)

&desel    .(
          ldx actdrv
          bmi sd1
          lda WD1+WD_TRCK
          sta tracks,x
sd1       lda #<-1
          sta actdrv
	; actually this does not work here, as
	; the DRVIO board only has a single select line
          clc  
          rts
          .)

&go2track .(
          sta tr
          cmp WD1+WD_TRCK
          beq gtr

          jsr lowdens
          
          jsr findtr
          bcc gtc
          
          lda #$04       ; restore mit verify
          jsr setstep
          bcs gts

          jsr findtr
          bcs gts
gtc       and #%00011000
          bne gts
          
          jsr highdens
gtr       clc
          rts
          
gts       jsr highdens
          lda #E_GT
          sec
          rts
          .)

findtr    .(
          lda tr
          sta WD1+WD_DATA
          
          ldx actdrv
          lda hd,x
          and #2
          beq gnd

          lda WD1+WD_TRCK
          pha
          lda #$10       ; stepping ohne verify
          jsr setstep
          pla
          sta WD1+WD_TRCK     ; und das ganze nochmal mit verify
          lda tr
          sta WD1+WD_DATA     ; das sind dann doppelt so viele steps

gnd       lda #$14       ; seek track
          jmp setstep
          .)

&restore  .(
          jsr forcend
          jsr lowdens
          lda #$00       ; restore
          jsr setstep
          pha
          jsr highdens
          lda #0
          sta tr
          pla
          rts
          .)

&setadr   sta zeid
          sty zeid+1
          clc
          rts

&read     .(
          sta sect
          stx data
#ifdef BSHOW
          lda #"R"
          jsr SEROUT
          txa
          jsr HEXOUT
#endif
          jsr forcend
          lda #ERRCNT
          sta errcnt
rdloop    lda sect
          sta WD1+WD_SECT
          ;sei
          ldx data       ; Anzahl 256-Byte-Bl”cke
          ldy #0
          lda #%10001000
          jsr wcmd
          lda WD1+WD_ST
rd1       bit CTRL
          bmi rde
          bvc rd1
          lda WD1+WD_DATA
          sta (zeid),y
          iny
          bne rd1
          inc zeid+1
          dex
          bne rd1
          clc
          ldx WD1+WD_ST
          ldy errcnt  
          ;cli
          ;rts
	  bcc rdee	; always
rde       dec errcnt
          bne rdloop
          lda #E_RD
          sec
          ldx WD1+WD_ST
rdee
          cli
#ifdef NMIDEV
	php
	pha
	lda #NMI_ON
	jsr CTRLNMI
	pla
	plp
#endif
          rts
          .)

&write    .(
          sta sect       ; ( x-reg die Anzahl 256-Byte-Bl”cke )
          stx data
#ifdef BSHOW
          lda #"W"
          jsr SEROUT
          txa
          jsr HEXOUT
#endif
          jsr forcend
          lda #ERRCNT
          sta errcnt
wrloop    lda sect
          sta WD1+WD_SECT
          ;sei
          ldx data       ; Anzahl 256-Byte-Bl”cke
          ldy #0
          lda #%10101000
          jsr wcmd
          lda WD1+WD_ST
rd1       bit CTRL
          bmi rde
          bvc rd1
          lda (zeid),y
          sta WD1+WD_DATA
          iny
          bne rd1
          inc zeid+1
          dex
          bne rd1
          clc
          lda WD1+WD_ST
          ;cli
          ;rts
	  bcc rdee	; always
rde       dec errcnt
          bne wrloop
          lda #E_WR
          sec
          ldx WD1+WD_ST
rdee      cli
#ifdef NMIDEV
	php
	pha
	lda #NMI_ON
	jsr CTRLNMI
	pla
	plp
#endif
          rts
          .)

&ftrack   .(
          sta divers          ; virgin
          stx data+4          ; Sektor-L„nge
          lda POT2-1,x
          sta data            ; Anzahl 256-Byte-Bl”cke
          lda #ERRCNT
          sta errcnt
errloop   ldy #0
          lda (zeid),y
          sta data+2          ; endsector
          iny
          sty sect
          jsr forcend
          ;sei
          lda #%11110100
          jsr wcmd
          lda WD1+WD_ST  ; intrq loeschen
          ldy #80
          ldx #$4e
f1a       bit CTRL
          bmi fey
          bvc f1a
          stx WD1+WD_DATA
          dey
          bne f1a
sloop     lda data
          sta data+1
          ldx #0
          ldy #12
f2        bit CTRL
          bmi fey   
          bvc f2
          stx WD1+WD_DATA
          dey
          bne f2
          ldx #$f5
          ldy #3
f3        bit CTRL
          bmi fey
          bvc f3
          stx WD1+WD_DATA
          dey
          bne f3
          ldx #$fe
f4        bit CTRL
fey       bmi fex
          bvc f4
          stx WD1+WD_DATA
          ldx tr
f5        bit CTRL
          bmi fex
          bvc f5
          stx WD1+WD_DATA
          ldx side
f6        bit CTRL
          bmi fex
          bvc f6
          stx WD1+WD_DATA
          ldy sect
          lda (zeid),y
          tax
f7        bit CTRL
          bmi fex
          bvc f7
          stx WD1+WD_DATA
          ldx data+4         ; l„nge des Sektors
f8        bit CTRL
          bmi fex
          bvc f8
          stx WD1+WD_DATA
          ldx #$f7
f9        bit CTRL
          bmi fex
          bvc f9
          stx WD1+WD_DATA
          ldy #22
          ldx #$4e
f10       bit CTRL
          bmi fex
          bvc f10
          stx WD1+WD_DATA
          dey
          bne f10
          ldy #12
          ldx #0
f11       bit CTRL
          bmi fex
          bvc f11
          stx WD1+WD_DATA
          dey
          bne f11
          ldy #3
          ldx #$f5
f12       bit CTRL
fex       bmi fe
          bvc f12
          stx WD1+WD_DATA
          dey
          bne f12
          ldx #$fb
f13       bit CTRL
          bmi fe
          bvc f13
          stx WD1+WD_DATA
          ldx divers          ; virgin
f15       ldy #0
f14       bit CTRL
          bmi fe
          bvc f14
          stx WD1+WD_DATA
          dey
          bne f14
          dec data+1
          bne f15
          ldx #$f7            ; end of sektor
f16       bit CTRL
          bmi fe
          bvc f16
          stx WD1+WD_DATA
          ldy #24
          ldx #$4e            ; pause nach sektor
f20       bit CTRL
          bmi fe
          bvc f20
          stx WD1+WD_DATA
          dey
          bne f20
          inc sect
          dec data+2
          beq f18a
          jmp sloop
f18a      ldx #$4e
f18       bit CTRL
          bmi fr
          bvc f18
          stx WD1+WD_DATA
          jmp f18        ; absolut
fe        dec errcnt
          beq fee
          jmp errloop
fee       sec
          lda #E_FT
          ldx WD1+WD_ST
	.byt $24
fr	sec
	cli
#ifdef NMIDEV
	php
	pha
	lda #NMI_ON
	jsr CTRLNMI
	pla
	plp
#endif
	rts
      .)


	.)

