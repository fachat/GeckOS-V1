
/****************************************************************************
   
    OS/A65 Version 1.3.11
    Multitasking Operating System for 6502 Computers

    Copyright (C) 1989-1996 Andre Fachat 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

****************************************************************************/

/*
 * This file is a TCP implementation for 6502 computer
 *
 * it exports:
 *	tcpinit		- init TCP
 *	tcploop		- must be called 
 *	tcprx		- gets incoming packets
 *
 *	tcp_open	- (active) open TCP connection
 *	tcp_listen	- (passive) open TCP connection
 *	tcp_close	- close TCP connection
 *	
 */

/* TODO: make the thing 32-bit-modulo save for ack and seq! */

#define	DEBUGTCP

#include "tcp.def"

	.(

tcbp	=syszp
p2	=syszp+2
srv	=p2		;srv	=syszp+4
-syszp	+=4		;-syszp	+=6

dlen	=sysmem
doffset	=sysmem+2	; data offset from ip!
toffset	=sysmem+3	; tcp header offset from ip!
seg_flag=sysmem+4	; incoming segment flag byte
;tslot	=sysmem+5	; incoming segment memory slot
discd	=sysmem+6	; data preceding valid data in segment
-sysmem	+=8
tcb	=sysmem
-sysmem	+=MAXCONN*TCB_LEN
phdr	=sysmem
-sysmem	+=TPH_LEN
conn	=sysmem
-sysmem	+=1
timer	=sysmem
-sysmem	+=2

&&tcpinit .(
	ldx #0

	stx timer
	stx timer+1
i1
	jsr settcb
	lda #TCP_CLOSED
	ldy #TCB_STATE
	sta (tcbp),y
	inx
	cpx #MAXCONN
	bcc i1

	jsr user_init

	clc
	rts
	.)

&&tcploop .(
fl	=sysmem
-sysmem	+=1

	/* I have - yet - no way to measure the time, so we do a simple loop */
	lda timer
	ora timer+1
	sta fl
	beq load
	lda timer
	bne l1
	dec timer+1
l1	dec timer
	jmp te
load	lda #<TIMER
	sta timer
	lda #>TIMER
	sta timer+1
te
	ldx #<-1
i1 	inx
	cpx #MAXCONN
	bcs end
	stx conn
	jsr settcb
	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_CLOSED
	beq i1

	ldy #TCB_LSTATE
	cmp (tcbp),y
	beq same
	sta (tcbp),y
	; ok, state has changed since last loop
	cmp #TCP_CLOSEW
	bne n1
	ldx conn
	jsr tcp_close	; TODO we should actually signal the user process...
n1
same	lda fl
	bne next

	ldy #TCB_MSL+1
	lda (tcbp),y
	dey
	ora (tcbp),y
	beq nomsl

	sec
	lda (tcbp),y
	sbc #1
	sta (tcbp),y
	tax
	iny
	lda (tcbp),y
	sbc #0
	sta (tcbp),y
	bne nomsl
	txa
	bne nomsl
	
	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_TIMEW
	bne nomsl
	lda #TCP_CLOSED
	sta (tcbp),y
nomsl
	ldy #TCB_RETRANS+1
	lda (tcbp),y
	dey
	ora (tcbp),y
	beq noretrans

	sec
	lda (tcbp),y
	sbc #1
	sta (tcbp),y
	tax
	iny
	lda (tcbp),y
	sbc #0
	sta (tcbp),y
	bne noretrans
	txa
	bne noretrans

	jsr retransmit
noretrans
next	ldx conn
	jmp i1
	
end	rts
	.)

&tcprx	.(

state	=sysmem
-sysmem	+=1

	/* make IP header to TCP pseudo header */
	lda #0
	sta phdr+TH_PTCL-1
	ldy #IPH_PROT
	lda (ip),y
	sta phdr+TH_PTCL
	ldy #IPH_SRC
c1	lda (ip),y
	sta phdr-IPH_SRC,y
	iny
	cpy #IPH_TRG+4
	bne c1
	ldy #TH_TCPLEN
	lda idlen+1
	sta phdr,y
	lda idlen
	sta phdr+1,y

	lda id
	sec
	sbc #TPH_LEN
	sta id
	lda id+1
	sbc #0
	sta id+1

	lda idlen
	clc
	adc #TPH_LEN
	sta idlen
	lda idlen+1
	adc #0
	sta idlen+1

	ldy #0
c2	lda phdr,y
	sta (id),y
	iny
	cpy #TPH_LEN
	bne c2

#if 0 /*def DEBUGTCP*/
DB("TCP check: id=")
lda id+1: jsr EHexout: lda id: jsr EHexout
DB(", len=")
lda id+3: jsr EHexout: lda id+2: jsr EHexout
jsr ECrlfout
#endif

	lda idlen
	ldy idlen+1
	ldx #id
	jsr checksum3

#if 0 /*def DEBUGTCP*/
php
pha
txa
pha
DB("TCP Checksum=")
pla
tay
jsr EHexout
pla
pha
jsr EHexout
jsr ECrlfout
tya
tax
pla
plp
#endif
	bcc tcpok

        DB("Packet discarded: TCP checksum^m^j")
        jmp discard

tcpok	/* here we have a valid TCP packet in (id),0-idlen. */

	lda #0		; data to be discarded at packet beginning.
	sta discd
	sta discd+1

	ldy #TH_FLAG
	lda (id),y
	sta seg_flag

	ldy #TH_DOFFSET
	lda (id),y
	and #$f0
	lsr
	lsr
	clc
	adc #TPH_LEN
	sta doffset
	lda idlen
	sec
	sbc doffset
	sta dlen
	lda idlen+1
	sbc #0
	sta dlen+1

	lda ipl+1
	bne datl
	lda ipl
	sec
	sbc #TPH_LEN
	sta toffset
	clc
	adc doffset
	sta doffset

	bcc looktcb
datl
	DB("Data offset too large^m^j")
	jmp discard

tmp	=sysmem
tmp2	=sysmem+1
-sysmem	+=4	; should be able to hold seq number

looktcb
	/* find TCB for this packet */
	.(
	ldx #<-1
next	inx
	cpx #MAXCONN
	bcc t1

nextl	dex
	bpl inuse

	DB("No Connection!^m^j")
	jmp discard

t1	jsr settcb
	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_LISTEN+1
	bcc next
	jmp search

inuse	jsr settcb		/* TODO: make listen not occupy a full TCB */
	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_LISTEN
	bne nextl		/* full search */

	ldy #TH_TRGP
iu2	lda (id),y
	cmp (tcbp),y
	bne nextl
	iny
	cpy #TH_TRGP+2
	bcc iu2
	; ok, we found one. now copy IP addresses and remote port

	ldy #TCB_SERVICE	/* check if multiple listens */
	lda (tcbp),y
	sta srv
	iny
	lda (tcbp),y
	sta srv+1
	ldy #SRV_FLAG
	lda (srv),y
	and #SFL_MULT
	beq nodup
				/* yes, then dup tcb */
	lda tcbp
	sta srv
	lda tcbp+1
	sta srv+1
	stx conn
	jsr gettcb
	bcc iu4
	ldx conn
	jmp nextl
iu4
	ldy #0
iu3	lda (srv),y
	sta (tcbp),y
	iny
	cpy #TCB_LEN
	bcc iu3
nodup
	ldy #TH_SRCIP
li1	lda (id),y
	sta (tcbp),y
	iny
	cpy #TH_TRGP+2
	bcc li1
	jmp ok

next2	jmp next

search	
	ldy #TH_SRCP		/* TCB_HDR must be 0! */
tl	lda (id),y
	cmp (tcbp),y
	bne next2
	iny
	cpy #TH_TRGP+2
	bcc tl

	ldy #TH_SRCIP
tm	lda (id),y
	cmp (tcbp),y
	bne next2
	iny
	cpy #TH_TRGIP+4
	bcc tm
ok
	.)

	stx conn

	lda islot
	sta qslot
	lda #<-1
	sta islot	; disable re-use of incoming packet

	ldy #TCB_STATE
	lda (tcbp),y
	sta state

#ifdef DEBUGTCP
	lda tcbp+1
	jsr EHexout
	lda tcbp
	jsr EHexout
	lda #":"
	jsr ECout

	lda state
	asl
	tax
	lda ttab+1,x
	tay
	lda ttab,x
	jsr ETxtout
	jmp noout

ttab	.word tclosed, tlisten, tsynrxd, tsyntxd, testab, tfinw1, tfinw2
	.word tclosew, tlastack, tclosing, ttimew

tclosed	.asc "Closed:^m^j",0
tlisten	.asc "Listen:^m^j",0
tsynrxd .asc "SynRXd:^m^j",0
tsyntxd .asc "SynTXd:^m^j",0
testab 	.asc "Established:^m^j",0
tfinw1	.asc "FinW1:^m^j",0
tfinw2	.asc "FinW2:^m^j",0
tclosew	.asc "CloseWait:^m^j",0
tlastack .asc "LastAck:^m^j",0
tclosing .asc "Closing:^m^j",0
ttimew	.asc "TimeWait^m^j",0

noout
#endif
	lda state
	asl
	tay
	
	lda stab+1,y
	pha
	lda stab,y
	pha
	rts

stab	.word 	tcp_closed-1, tcp_listen-1, tcp_synrxd-1, tcp_syntxd-1
	.word	tcp_estab-1, tcp_finw1-1,  tcp_finw2-1,  tcp_closew-1
	.word   tcp_lastack-1, tcp_closing-1, tcp_timew-1

tcp_closed .(
	lda seg_flag
	and #THF_RST
	bne end
	lda seg_flag
	and #THF_ACK
	bne ackset

	lda #0
	ldy #TH_SEQ
l0	sta (id),y
	iny
	cpy #TH_SEQ+4
	bcc l0

	clc
	ldy #TH_SEQ+3
	lda (id),y
	adc dlen
	ldy #TH_ACK+3
	sta (id),y
	ldy #TH_SEQ+2
	lda (id),y
	adc dlen+1
	ldy #TH_ACK+2
	sta (id),y
	ldy #TH_SEQ+1
	lda (id),y
	adc #0
	ldy #TH_ACK+1
	sta (id),y
	ldy #TH_SEQ
	lda (id),y
	adc #0
	ldy #TH_ACK
	sta (id),y

	lda #THF_ACK + THF_RST
	bne noack

&send_rst
&listen_ack
ackset	ldy #TH_ACK
	lda (id),y
	ldy #TH_SEQ
	sta (id),y
	ldy #TH_ACK+1
	lda (id),y
	ldy #TH_SEQ+1
	sta (id),y
	ldy #TH_ACK+2
	lda (id),y
	ldy #TH_SEQ+2
	sta (id),y
	ldy #TH_ACK+3
	lda (id),y
	ldy #TH_SEQ+3
	sta (id),y

	lda #THF_RST
noack
	ldy #TH_FLAG
	sta (id),y

	jmp bangbuf
	
end	jmp tdiscard
	.)

tcp_listen .(
	lda seg_flag
	and #THF_RST
	bne listen_rst

	lda seg_flag
	and #THF_ACK
	bne listen_ack

	lda seg_flag
	and #THF_SYN
	bne listen_syn
	jmp nosyn
&listen_syn
	DB("Listen_syn^m^j")
#if 0
	jsr rxhdr
	bcc okhdr
	jmp disc
okhdr
#endif
	jsr seqsyn

	jsr hasdata
	beq nodat
	jsr copy_n_queue
nodat
	lda #0
	tay
	jsr chkbuf
	bcc short

	ldy #TH_FLAG
	lda #THF_SYN + THF_ACK
	sta (id),y

	jsr iniseq

	ldy #TCB_STATE
	lda #TCP_SYNRXD
	sta (tcbp),y

	jsr setack
	jsr incuna

	jmp bangbuf

&reset
	DB("Reset rxd!^m^j")
	ldy #TCB_STATE
	lda #TCP_SYNRXD
	sta (tcbp),y
	jmp tdiscard

listen_rst
	DB("Reset while listening^m^j")
	jmp tdiscard
short	DB("Syn packet short^m^j")
	jmp tdiscard
nosyn	DB("Listen but no Syn!^m^j")
disc	jmp tdiscard
	.)

tcp_estab
tcp_finw1  tcp_finw2  tcp_closew tcp_lastack tcp_closing
tcp_timew
tcp_synrxd .(

needack	=sysmem
-sysmem	+=1

	lda #0
	sta needack

	/* first, check sequence number */
	jsr checkseq
	bcc seqok

	lda seg_flag
	and #THF_RST
	bne disca	/* discard packet */

	jsr setseq
	jsr setack
	ldy #TH_FLAG
	lda #THF_ACK
	sta (id),y
	jmp bangbuf

disca	jmp tdiscard

seqok	/* second, check the RST bit */
	lda seg_flag
	and #THF_RST
	beq noreset	

	lda state
	cmp #TCP_SYNRXD
	bne seq1

	/* TODO: if from active open, then close! */
	jmp reset	

seq1	cmp #TCP_LASTACK
	bcs seq2

	/* TODO: flush and close everything */
	jmp tdiscard

seq2	/* TODO: close */
	jmp tdiscard

noreset	/* third, check security and precedence... */

	/* fourth, check SYN bit */

	lda seg_flag
	and #THF_SYN
	beq nosyn
	; this is an error - abort anything

	/* TODO: flush everything, close */
	ldy #TCB_STATE
	lda #TCP_CLOSED
	sta (tcbp),y
	jmp send_rst

nosyn	/* fifth, check the ack bit */
	lda seg_flag
	and #THF_ACK
	bne ackok

	DB("No Ack^m^j")
	jmp tdiscard
ackok
	jsr checkack
	sta tmp

	lda state
	cmp #TCP_SYNRXD
	bne ack1

	lda tmp
	beq badack
	cmp #3
	bcc ackok2

badack	DB("Bad Ack in packet^m^j")
	jmp send_rst
ackok2
	ldy #TCB_STATE	/* everything ok -> enter estab and continue... */
	lda #TCP_ESTAB
	sta (tcbp),y
	sta state

	/* TODO: check queued FIN */

ack1	lda tmp
	beq aignore
	cmp #3
	bcc a0

	/* TODO: ack ahead send_next -> send ack, return */
	DB("Ack ahead send_next^m^j")
	jmp tdiscard
a0
	ldy #TH_ACK	/* copy 2 SND_UNA */
a1	lda (id),y
	sta (tcbp),y
	iny
	cpy #TH_ACK+4
	bcc a1

	jsr ackxmit
aignore
	lda state
	cmp #TCP_FINW1
	bcc do6
	bne ack3

	lda tmp
	cmp #2		; exactly SND_NXT -> FIN ack'd
	bne do6
	ldy #TH_FLAG
	lda #TCP_FINW2
	sta (tcbp),y
	sta state

ack3	cmp #TCP_FINW2
	bne ack4
	/* TODO: ack user close */
	jmp do6

ack4	cmp #TCP_CLOSING
	bne ack5

	lda tmp
	cmp #2
	bne atw
	ldy #TCB_STATE
	lda #TCP_TIMEW
	sta state
	sta (tcbp),y

	ldy #TCB_MSL
	lda #<TIME_MSL
	sta (tcbp),y
	iny
	lda #>TIME_MSL
	sta (tcbp),y

atw	jmp tdiscard

ack5	cmp #TCP_LASTACK
	bne ack6
	
	lda tmp
	cmp #2
	bne atw
	ldy #TCB_STATE
	lda #TCP_CLOSED
	sta (tcbp),y
	jmp atw

ack6	cmp #TCP_TIMEW
	bne do6

	ldy #TCB_MSL
	lda #<TIME_MSL
	sta (tcbp),y
	iny
	lda #>TIME_MSL
	sta (tcbp),y
	
	; ack retransmitted FIN 
	jsr setack
	jsr setseq
	ldy #TH_FLAG
	lda #THF_ACK
	sta (id),y
	jmp bangbuf

	/* sixth, check the URG bit */
do6
	/* seventh, process segment text */ 
	
	lda state
	cmp #TCP_ESTAB
	bcc idat
	cmp #TCP_FINW2+1
	bcs idat

	ldy #0
	lda doffset
	sta (ip),y
	tya
	iny
	sta (ip),y
	iny
	lda dlen
	sta (ip),y
	iny
	lda dlen+1
	sta (ip),y

	jsr hasdata
	beq idat
	jsr copy_n_queue	; ok, we need not copy without FIN...
	inc needack
idat	

	/* eighth, check the FIN bit */
	
	lda state
	cmp #TCP_CLOSED
	beq nofin
	cmp #TCP_LISTEN
	beq nofin
	cmp #TCP_SYNTXD
	beq nofin

	lda seg_flag
	and #THF_FIN
	beq nofin

	lda #1
	ldx #0
	jsr addrxnxt

	inc needack

	jsr setack
	jsr setseq
	ldy #TH_FLAG
	lda #THF_ACK
	sta (id),y
	jsr bangbuf

	lda state
	cmp #TCP_ESTAB+1
	bcs f1
	lda #TCP_CLOSEW
	bne fe

fe	ldy #TCB_STATE
	sta (tcbp),y

f1	
	/* TODO: check other states */

nofin	
	ldx qslot
	bmi noslot

	lda needack
	beq tdisc
	
	jsr setack
	jsr setseq
	ldy #TH_FLAG
	lda #THF_ACK
	sta (id),y
	jmp bangbuf	; maybe check with retransmissions...

tdisc	ldx qslot
	bmi noslot
	jsr bfree
noslot	clc
	rts
	.)

tcp_syntxd .(
	DB("SynTxd^m^j")
jmp tdiscard
#if 0
	lda seg_flag
	pha

	jsr rxhdr
	bcs discp
	pla
	and #THF_SYN + THF_ACK
	beq disc
	cmp #THF_ACK
	beq disc
	pha		; now SYN is set

	/* TODO: send ACK */

	pla
	cmp #THF_SYN
	beq noack
	; here SYN and ACK are set
	lda #TCP_ESTAB
	.byt $2c
noack	lda #TCP_SYNRXD
	ldy #TCB_STATE
	sta (tcbp),y
	rts

discp	pla
disc	jmp discard
#endif
	.)

tdiscard .(
	ldx qslot
	jsr bfree
&end2	clc
	rts
	.)

/* check acknowledge of packets in retransmission queue */
/* TODO: save end pointer for each packet in queue -> no calculations here */
ackxmit	.(
DB("ackxmit:")
.(:ldy #TCB_SND_UNA: x1 lda (tcbp),y: jsr EHexout: iny: cpy #TCB_SND_UNA+4:bcc x1: .)
lda #":":jsr ECout

	ldy #TCB_NTXBUF
	lda (tcbp),y
	beq end2

	ldy #TCB_TXBUF
	lda (tcbp),y
	tax
pha: DB("slot:"): pla: jsr EHexout: lda #":": jsr ECout
	jsr getbadr
	sta p2
	sty p2+1

	ldx #3
	ldy #TH_SEQ+TCP_OFFSET+3
l0	lda (p2),y
	sta tmp,x
jsr EHexout
	dey
	dex
	bpl l0

	ldy #IPH_LEN+1
	lda (p2),y
	clc
	adc tmp+3
	sta tmp+3
	dey
	lda (p2),y
	adc tmp+2
	sta tmp+2
	bcc l1
	inc tmp+1
	beq l1
	inc tmp
l1	lda tmp+3
	sec
	sbc #<TCP_OFFSET+TH_OPTIONS
	sta tmp+3
	lda tmp+2
	sbc #>TCP_OFFSET+TH_OPTIONS
	sta tmp+2
	bcs l2
	lda tmp
	bne l3
	dec tmp+1
l3	dec tmp
l2

lda #":": jsr ECout
.(:ldy #0: x1 lda tmp,y: jsr EHexout: iny: cpy #4:bcc x1: .)
jsr ECrlfout

	ldy #TCB_SND_UNA
l4	lda (tcbp),y
	cmp tmp-TCB_SND_UNA,y
	bcc end
	iny
	cpy #TCB_SND_UNA+4
	bcc l4

	; got ack for first packet in queue
	ldy #TCB_TXBUF
	lda (tcbp),y
	tax
	jsr bfree

	ldy #TCB_NTXBUF
	lda (tcbp),y
	tax
	dex
	txa
	sta (tcbp),y
	beq end		; ok, end, nothing left

	ldy #TCB_TXBUF
l5	iny
	lda (tcbp),y
	dey
	sta (tcbp),y
	iny
	dex
	bne l5

	jmp ackxmit

end	rts
	.)

seqsyn	.(
	ldy #TH_SEQ+3
	clc
	lda (id),y
	adc #1
	sta (id),y
	sta (tcbp),y
	dey 
	lda (id),y
	adc #0
	sta (id),y
	sta (tcbp),y
	dey
	lda (id),y
	adc #0
	sta (id),y
	sta (tcbp),y
	dey
	lda (id),y
	adc #0
	sta (id),y
	sta (tcbp),y
	rts
	.)

#if 0
rxhdr	.(
	lda seg_flag
	and #THF_SYN
	beq nosyn

	ldy #TH_SEQ+3
	clc
	lda (id),y
	adc #1
	sta (id),y
	sta (tcbp),y
	dey 
	lda (id),y
	adc #0
	sta (id),y
	sta (tcbp),y
	dey
	lda (id),y
	adc #0
	sta (id),y
	sta (tcbp),y
	dey
	lda (id),y
	adc #0
	sta (id),y
	sta (tcbp),y
nosyn
	ldy #TH_SEQ	/* we don't handle disordered packets - ignore them */
seql	lda (id),y
	cmp (tcbp),y
	bne seqmismatch
	iny
	cpy #TH_SEQ+4
	bcc seql

	lda seg_flag
	and #THF_ACK
	beq noack
	ldy #TH_ACK	/* TODO: ack larger than send next ? -> send reset */
ackl 	lda (id),y
	sta (tcbp),y
	iny
	cpy #TH_ACK+4
	bcc ackl	/* TODO: remove from retransmission queue */
noack
	jmp doopts

&seqmismatch
	DB("Sequence number mismatch^m^j")
	sec
	rts
	.)
#endif

checkseq .(
	ldy #TH_SEQ	/* we don't handle disordered packets - ignore them */
seql	lda (id),y
	cmp (tcbp),y
	bcc lesscheck
	bne seqmm1
next 	iny
	cpy #TH_SEQ+4
	bcc seql
	clc
	rts
lesscheck
	ldy #TH_SEQ+3
	lda (id),y
	clc
	adc dlen
	sta tmp+3
	dey
	lda (id),y
	adc dlen+1
	sta tmp+2
	dey
	lda (id),y
	adc #0
	sta tmp+1
	dey
	lda (id),y
	adc #0
	sta tmp

	ldy #TCB_RCV_NXT
seqt	lda tmp-TCB_RCV_NXT,y
	cmp (tcbp),y
	bcc seqmm1
	bne setdiscd
	iny
	cpy #TCB_RCV_NXT+4
	bcc seqt
seqmm1	DB("Sequence mismatch^m^j")
	sec
	rts

setdiscd
	ldx #4
	ldy #TCB_RCV_NXT+3
	sec
s1	lda (tcbp),y
	sbc (id),y
	sta tmp-TCB_RCV_NXT,y
	dey
	dex
	bne s1

	lda tmp
	ora tmp+1
	bne seqmm1

	lda tmp+2
	sta discd+1
	lda tmp+3
	sta discd
	clc
	rts
	.)

#if 0
doopts	.(
	ldy #TH_DOFFSET
	lda (id),y
	and #$f0
	cmp #$60
	bcc no_options
	lsr
	lsr
	clc
	adc #TH_SRCP
	sta tmp2
	ldy #TH_OPTIONS
optloop
	sty tmp
	lda (id),y
	beq no_options
	iny
	cmp #1
	beq optloop
	cmp #2
	bne ill_options	; - ignore illegal options and all following
	; max segment size
	lda (id),y
	pha		; option length
	iny
	lda (id),y	; high
	tax
	iny
	lda (id),y	; low byte
	cpx #>TCPSEG
	bcc oklen
	bne newlen
	cmp #<TCPSEG
	bcc oklen
newlen	lda #<TCPSEG
	ldx #>TCPSEG
oklen 	ldy #TCB_MAXSEG
	sta (tcbp),y
	iny
	txa
	sta (tcbp),y
	pla
	clc
	adc tmp
	tay
	jmp optloop

ill_options
	jsr EHexout
	DB("Illegal header option ^m^j")
	
no_options
	clc
	rts
	.)
#endif


d2len	=sysmem
-sysmem	+=2

hasdata	.(
	lda dlen
	ora dlen+1
	; bne yes
	rts
	.)

chkbuf	.(
	sta d2len
	sty d2len+1
	lda idlen
	sec
	sbc d2len
	tax
	lda idlen+1
	sbc d2len+1
	bne ret
	cpx #TPH_LEN+24
	;bcc no
ret	rts
	.)

checkack .(
	/* first check with SND_UNA - if less then ignore */
	ldy #TH_ACK
l1	lda (id),y
	cmp (tcbp),y
	bcc badack0
	iny
	cpy #TH_ACK+4
	bcc l1

	lda tcbp
	clc
	adc #4
	sta p2
	lda tcbp+1
	adc #0
	sta p2+1

	/* now check with SND_NXT - if greater then error */
	ldy #TH_ACK
l2	lda (p2),y
	cmp (id),y
	bcc badack1
	iny
	cpy #TH_ACK+4
	bcc l2

	/* now check for equality with SND_NXT */
	ldy #TH_ACK+3
l3	lda (p2),y
	cmp (id),y
	bne okack1
	dey
	cpy #TH_ACK
	bcs l3
	lda #2		; exactly SND_NXT
	.byt $2c
okack1	lda #1		; not exactly SND_NXT
	clc
	rts
badack0	lda #0		; old acknowledge
	.byt $2c
badack1	lda #3		; ack ahead
	sec
	rts
	.)

iniseq	.(
	ldy #TCB_SND_NXT
	lda $dc07
	sta (tcbp),y
	iny
	lda $dc06
	sta (tcbp),y
	iny
	lda $dd07
	sta (tcbp),y
	iny
	lda $dd06
	sta (tcbp),y

	sec
	ldy #TCB_SND_NXT+3
	lda (tcbp),y
	sbc #1
	ldy #TCB_SND_UNA+3
	sta (tcbp),y
	ldy #TH_SEQ+3
	sta (id),y
	ldy #TCB_SND_NXT+2
	lda (tcbp),y
	sbc #0
	ldy #TCB_SND_UNA+2
	sta (tcbp),y
	ldy #TH_SEQ+2
	sta (id),y
	ldy #TCB_SND_NXT+1
	lda (tcbp),y
	sbc #0
	ldy #TCB_SND_UNA+1
	sta (tcbp),y
	ldy #TH_SEQ+1
	sta (id),y
	ldy #TCB_SND_NXT
	lda (tcbp),y
	sbc #0
	ldy #TCB_SND_UNA
	sta (tcbp),y
	ldy #TH_SEQ
	sta (id),y
	rts
	.)

bangbuf	.(

	; first exchange port and IP addresses
	lda id
	clc
	adc #TH_TRGP - TH_SRCP
	sta p2
	lda id+1
	adc #0
	sta p2+1

	ldy #TH_SRCP
l1	lda (id),y
	tax
	lda (p2),y
	sta (id),y
	txa
	sta (p2),y
	iny
	cpy #TH_SRCP+2
	bcc l1

	lda id
	clc
	adc #TH_TRGIP - TH_SRCIP
	sta p2
	lda id+1
	adc #0
	sta p2+1

	ldy #TH_SRCIP
l2	lda (id),y
	tax
	lda (p2),y
	sta (id),y
	txa
	sta (p2),y
	iny
	cpy #TH_SRCIP+4
	bcc l2

	; now check syn and add max seg size option if it's syn
	lda seg_flag
	and #THF_SYN
	beq nosyn
	ldy #TH_OPTIONS
	lda #2
	sta (id),y
	iny
	lda #4
	sta (id),y
	iny
	lda #>TCPWIN
	sta (id),y
	iny
	lda #<TCPWIN
	sta (id),y

	lda #$60
	.byt $2c
nosyn	lda #$50
	ldy #TH_DOFFSET
	sta (id),y

	lsr
	lsr
	ldy #TH_TCPLEN+1
	sta (id),y
	clc
	adc #TPH_LEN
	sta ql
	dey
	lda #0
	sta (id),y
	adc #0
	sta ql+1

	lda ip
	sta qp
	lda ip+1
	sta qp+1

	lda id
	sta qd
	lda id+1
	sta qd+1

/*DB("from bangbuf: ")*/
&mkpacket

DB("mkpacket: qp=")
lda qp+1: jsr EHexout: lda qp: jsr EHexout
DB(", qd=")
lda qd+1: jsr EHexout: lda qd: jsr EHexout
DB(", ql=")
lda ql+1: jsr EHexout: lda ql: jsr EHexout
jsr ECrlfout

	; this is for other packets also...
	ldy #TH_WINDOW
	lda #>TCPWIN
	sta (qd),y
	iny
	lda #<TCPWIN
	sta (qd),y

	lda #0
	ldy #TH_CHECK
	sta (qd),y
	iny
	sta (qd),y
	
	lda ql
	ldy ql+1
	ldx #qd
	jsr checksum3
	ldy #TH_CHECK+1
	sta (qd),y
	dey
	txa
	sta (qd),y

	; make IP header from TCP pseudo header
	; (i.e. copy the IP addresses to the right location and set protocol
	; the rest is done in the IP layer

	lda qp
	clc
	adc #TCP_OFFSET
	tax
	lda qp+1
	adc #0
	cmp qd+1
	bne move
	cpx qd
	beq nomove
move	DB("packet start mismatch!^m^jip=")
	lda qp+1: jsr EHexout: lda qp: jsr EHexout
	DB(", id=")
	lda id+1: jsr EHexout: lda id: jsr EHexout
	jmp tdiscard
nomove

	lda ql
	sec
	sbc #<TPH_LEN
	sta qlen
	lda ql+1
	sbc #>TPH_LEN
	sta qlen+1

	lda qd
	clc
	adc #4
	sta p2
	lda qd+1
	adc #0
	sta p2+1

	ldy #TH_TRGIP+3
l3	lda (qd),y
	tax
	lda (p2),y
	sta (qd),y
	txa
	sta (p2),y
	dey
	cpy #<TH_SRCIP-1
	bne l3

	ldy #1		; IP_PROT
	lda #6
	sta (qp),y

#if 0 /*def DEBUGTCP*/
	DB("Send Seq: ")
	ldy #TH_SEQ
x1	lda (qd),y
	jsr EHexout
	iny
	cpy #TH_SEQ+4
	bcc x1
	DB("^m^jSend Ack: ")
	ldy #TH_ACK
x2	lda (qd),y
	jsr EHexout
	iny
	cpy #TH_ACK+4
	bcc x2
	DB("^m^jFlag= ")
	ldy #TH_FLAG
	lda (qd),y
	jsr EHexout
	jsr ECrlfout
#endif
	jmp sendip
	.)

	/* add a/x to tx_nxt */
addtxnxt .(
	ldy #TCB_SND_NXT+3
	clc
	adc (tcbp),y
	sta (tcbp),y
	dey
	txa
	adc (tcbp),y
	sta (tcbp),y
	dey
	ldx #2
l5	lda (tcbp),y
	adc #0
	sta (tcbp),y
	dey
	dex
	bne l5
	rts
	.)

	/* add a/x to rx_nxt */
addrxnxt .(
	ldy #TCB_RCV_NXT+3
	clc
	adc (tcbp),y
	sta (tcbp),y
	dey
	txa
	adc (tcbp),y
	sta (tcbp),y
	dey
	ldx #2
l5	lda (tcbp),y
	adc #0
	sta (tcbp),y
	dey
	dex
	bne l5
	rts
	.)

incuna	.(
	ldy #TCB_SND_UNA+3
	ldx #3
	lda (tcbp),y
	clc
	adc #1
	sta (tcbp),y
	dey
l5	lda (tcbp),y
	adc #0
	sta (tcbp),y
	dey
	dex
	bne l5
	rts
	.)

setseq	.(
	lda tcbp
	clc
	adc #8
	sta p2
	lda tcbp+1
	adc #0
	sta p2+1

	ldy #TH_SEQ
l5	lda (p2),y
	sta (id),y
	iny
	cpy #TH_SEQ+4
	bcc l5
	rts
	.)

setack	.(
	lda id
	clc
	adc #4
	sta p2
	lda id+1
	adc #0
	sta p2+1

	ldy #TCB_RCV_NXT
l5	lda (tcbp),y
	sta (p2),y
	iny
	cpy #TCB_RCV_NXT+4
	bcc l5
	rts
	.)

setqseq	.(
	lda tcbp
	clc
	adc #8
	sta p2
	lda tcbp+1
	adc #0
	sta p2+1

	ldy #TH_SEQ
l5	lda (p2),y
	sta (qd),y
	iny
	cpy #TH_SEQ+4
	bcc l5
	rts
	.)

setqack	.(
	lda qd
	clc
	adc #4
	sta p2
	lda qd+1
	adc #0
	sta p2+1

	ldy #TCB_RCV_NXT
l5	lda (tcbp),y
	sta (p2),y
	iny
	cpy #TCB_RCV_NXT+4
	bcc l5
	rts
	.)

/* The following routine takes the packet from 
 * islot/ip/id/ipl/idlen/dlen/doffset/toffset
 * and queues it to the user data rx queue.
 * It sets ack appropriately. 
 * It copies the TCP header into a new packet and sets
 * islot/ip/..., but dlen is zero.
 */
copy_n_queue .(
myslot	=sysmem
-sysmem	+=1
myp	=syszp
-syszp	+=2

/*
DB("copy_n_queue: ip=")
lda ip+1: jsr EHexout: lda ip: jsr EHexout
DB(", request ")
lda doffset: jsr EHexout
DB(" b.^m^j")
*/
	lda doffset
	ldy #0
	jsr balloc
	bcc gotbuffer
	rts
gotbuffer
	stx myslot
	jsr getbadr
	sta myp
	sty myp+1
/*
DB("got buffer slot ")
lda myslot: jsr EHexout: DB(" @ address ")
lda myp+1: jsr EHexout: lda myp: jsr EHexout
jsr ECrlfout
*/
	ldy #0
l0	lda (ip),y
	sta (myp),y
/*jsr EHexout*/
	iny
	cpy doffset
	bne l0

	ldy #0
	lda doffset
	clc
	adc discd
	sta (ip),y		/* save for queue routines */
	tya
	adc discd+1
	iny
	sta (ip),y
	iny
	lda dlen
	sec
	sbc discd
	sta (ip),y
	iny
	lda dlen+1
	sbc discd+1
	sta (ip),y

/*lda #" ": jsr ECout: tya: jsr EHexout*/

	lda myp
	sta ip
	clc
	adc toffset
	sta id
	lda myp+1
	sta ip+1
	adc #0
	sta id+1

	lda doffset
	sec
	sbc toffset
	sta idlen
	lda #0
	sta idlen+1

	lda qslot
	jsr rx_queue_packet
	bcs boom

	lda dlen
	ldx dlen+1
	jsr addrxnxt
boom
	lda #0
	sta dlen
	sta dlen+1

	lda myslot
	sta qslot
/*
DB("copy_n_queue returns ip=")
lda ip+1: jsr EHexout: lda ip: jsr EHexout
DB(", id=")
lda id+1: jsr EHexout: lda id: jsr EHexout
jsr ECrlfout
*/
	clc
nobuffer
	rts
	.)

/* retransmit the first packet in the retransmission queue */
&retransmit .(
	ldy #TCB_NTXBUF
	lda (tcbp),y
	beq ende

	ldy #TCB_TXBUF
	lda (tcbp),y
	sta qslot
	tax

	ldy #TCB_TRIES
	lda (tcbp),y
	clc
	adc #1
	cmp #6		; retransmit 6 times only ?
	bcs ende
	jsr iniretrans

	jsr incownr
	jsr queueslot
ende	rts
	.)

iniretrans .( 
	ldy #TCB_TRIES
	sta (tcbp),y

	ldy #TCB_RETRANS
	lda #<TIME_RETRANS
	sta (tcbp),y
	iny
	lda #>TIME_RETRANS
	sta (tcbp),y

	rts
	.)

/* this routine puts outgoing packets into the retransmission queue and sends
 * them. It needs tcbp, and the packet buffer number in x
 */
&tx_queue_packet .(
	stx qslot
	jsr getbadr
	sta qp
	sty qp+1

	clc
	adc #TCP_OFFSET
	sta qd
	tya
	adc #0
	sta qd+1

	ldy #TCB_NTXBUF
	lda (tcbp),y
	cmp #TCP_MAXTXB
	bcc queueok
	rts
queueok
	adc #1
	sta (tcbp),y
	clc
	adc #TCB_TXBUF-1
	tay
	lda qslot
	sta (tcbp),y

	cpy #TCB_TXBUF
	bne notime

	lda #1
	jsr iniretrans

notime
	ldx qslot
	jsr incownr

	ldy #0
	lda (qp),y
	cmp #<TCP_OFFSET+TH_OPTIONS
	bne move
	iny
	lda (qp),y
	cmp #>TCP_OFFSET+TH_OPTIONS
	beq ok

move	DB("TCP offset wrong!^m^j")
	ldx qslot
	lda #<-1
	sta qslot
	jmp bfree

ok	iny
	lda (qp),y
	sta qlen	; len
	clc
	adc #<TH_OPTIONS
	sta ql		; TCP_LEN
	iny
	lda (qp),y
	sta qlen+1
	adc #>TH_OPTIONS
	sta ql+1

	jsr setout
	jsr setqseq
	jsr setqack

	lda qlen
	ldx qlen+1
	jsr addtxnxt		; inc seq number

	jmp mkpacket
	.)

setout	.(
	; make TCP packet from data
	clc
	lda #4
	adc qd
	sta p2
	lda #0
	adc qd+1
	sta p2+1

	ldy #TH_SRCIP
l0	lda (tcbp),y
	sta (p2),y
	iny
	cpy #TH_SRCIP+4
	bcc l0

	sec
	lda qd
	sbc #4
	sta p2
	lda qd+1
	sbc #0
	sta p2+1

	ldy #TH_TRGIP
l1	lda (tcbp),y
	sta (p2),y
	iny
	cpy #TH_TRGIP+4
	bcc l1

	ldy #TCB_SRCP
	lda (tcbp),y
	tax
	iny
	lda (tcbp),y
	ldy #TH_TRGP+1
	sta (qd),y
	dey
	txa
	sta (qd),y

	ldy #TCB_TRGP
	lda (tcbp),y
	tax
	iny
	lda (tcbp),y
	ldy #TH_SRCP+1
	sta (qd),y
	dey
	txa
	sta (qd),y

	ldy #TH_PTCL-1
	lda #0
	sta (qd),y
	iny
	lda #6
	sta (qd),y
	iny 
	iny
	lda ql
	sec
	sbc #<TPH_LEN
	sta (qd),y
	dey
	lda ql+1
	sbc #>TPH_LEN
	sta (qd),y

	ldy #TH_DOFFSET
	lda #$50
	sta (qd),y
	iny
	lda #THF_ACK
	sta (qd),y

	rts
	.)

&send_reset
	lda #THF_RST
	.byt $2c
&send_fin
	lda #THF_FIN+THF_ACK
	.(
	sta tmp
	lda #<TH_OPTIONS+TCP_OFFSET
	ldy #>TH_OPTIONS+TCP_OFFSET
	jsr balloc
	bcc ok
	rts
ok	stx qslot
	sta qp
	sty qp+1

	clc
	adc #TCP_OFFSET
	sta qd
	tya
	adc #0
	sta qd+1

	lda #<TH_OPTIONS
	sta ql
	lda #>TH_OPTIONS
	sta ql+1

	jsr setout
	jsr setqseq
	jsr setqack

	lda tmp
	and #THF_FIN
	beq nofin

	lda #1
	ldx #0
	jsr addtxnxt
nofin
	ldy #TH_FLAG
	lda tmp
	sta (qd),y

	jmp mkpacket
	.)

	.)

settcb	.(
#if (TCB_LEN - 64)
	lda #0
	sta tcbp+1
	txa
	asl
	rol tcbp+1
	asl
	rol tcbp+1
	asl
	rol tcbp+1
	asl
	rol tcbp+1
	asl
	rol tcbp+1
	asl
	rol tcbp+1
	clc
	adc #<tcb
	sta tcbp
	lda #>tcb
	adc tcbp+1
	sta tcbp+1
	rts
#else
#warning TCB length not correct!
#endif
	.)

gettcb	.(
	ldx #0
l0	jsr settcb
	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_CLOSED
	beq l1
	inx
	cpx #MAXCONN
	bcc l0
	sec
	rts
l1	ldy #TCB_NTXBUF
	lda #0
	sta (tcbp),y
	ldy #TCB_FLAG
	sta (tcbp),y
	ldy #TCB_LSTATE
	lda #TCP_CLOSED
	sta (tcbp),y
	clc
	rts
	.)

/***************************************************************************
 * Here follow the user routines (open, close etc)
 */


tcbno	=sysmem
-sysmem	+=1

	; server struct:
	; remote_ip, remote_port, local_port, flag, queue_packet
echo_srv 
	.byte 0,0,0,0, 0,0, 7,0, SFL_MULT, <tcp_echo_queue-1, >tcp_echo_queue-1
discard_srv 
	.byte 0,0,0,0, 0,0, 9,0, SFL_MULT, <tcp_disc_queue-1, >tcp_disc_queue-1

+user_init .(
	lda #<echo_srv
	ldy #>echo_srv
	jsr tcp_listen
	lda #<discard_srv
	ldy #>discard_srv
	jsr tcp_listen
	rts
	.)

rx_queue_packet .(
	tax

	ldy #TCB_SERVICE
	lda (tcbp),y
	sta srv
	iny
	lda (tcbp),y
	sta srv+1

	ldy #SRV_QUEUE+1
	lda (srv),y
	pha
	dey
	lda (srv),y
	pha

	rts
	.)

+tcp_abort .(
	jsr settcb
	bcs notcb

	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_LISTEN
	beq del
	cmp #TCP_SYNTXD
	beq del

	cmp #TCP_CLOSING
	bcs ok
	jsr send_reset
del
	lda #TCP_CLOSED
	sta (tcbp),y
	clc
	rts
notcb	lda #E_NOCONN
	.byt $2c
closing	lda #E_CLOSING
	sec
	rts
ok	clc
	rts
	.)

+tcp_close .(
	jsr settcb
	bcs notcb

	ldy #TCB_STATE
	lda (tcbp),y
	cmp #TCP_LISTEN
	beq del
	cmp #TCP_SYNTXD
	beq del

	cmp #TCP_SYNRXD
	bne c1
	
	ldy #TCB_NTXBUF
	lda (tcbp),y
	beq c1a
	ldy #TCB_FLAG
	lda (tcbp),y
	ora #1
	sta (tcbp),y
	clc
	rts

c1	cmp #TCP_ESTAB
	bne c2
c1a	jsr send_fin
	ldy #TCB_STATE
	lda #TCP_FINW1
	sta (tcbp),y
	clc
	rts

c2	cmp #TCP_CLOSEW
	bne closing
	jsr send_fin
	ldy #TCB_STATE
	lda #TCP_CLOSING
	sta (tcbp),y
	clc
	rts
del
	lda #TCP_CLOSED
	sta (tcbp),y
	clc
	rts
notcb	lda #E_NOCONN
	.byt $2c
closing	lda #E_CLOSING
	sec
	rts
	.)

+tcp_listen .(
	sta srv
	sty srv+1
	jsr gettcb
	bcs notcb

	ldy #TCB_SERVICE
	lda srv
	sta (tcbp),y
	iny
	lda srv+1
	sta (tcbp),y

	ldy #SRV_LPORT
	lda (srv),y
	tax
	iny
	lda (srv),y
	ldy #TCB_TRGP
	sta (tcbp),y
	iny
	txa
	sta (tcbp),y
	
	ldy #TCB_STATE
	lda #TCP_LISTEN
	sta (tcbp),y
	clc
	;rts
notcb	rts
	.)

#include "tcpsrv.a65"

	.)

