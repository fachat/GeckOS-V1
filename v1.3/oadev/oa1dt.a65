/****************************************************************************
   
    OS/A65 Version 1.3.9
    Multitasking Operating System for 6502 Computers

    Copyright (C) 1989-1996 Andre Fachat 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

****************************************************************************/


#ifndef C64
#define   PIO       $e810
#define   VIA1      $e840
#define   vdcctrl   $e888
#define   vdc       $e880
#define   VIDPAGE   1
#endif

#define   maxzeil   #25

#ifndef C64
#define	KEY_START   24
#else
#define KEY_START   16
#endif

          .(

          .(
#define   ANZSCR         4
#define   DC_SW_MOD      $20
#define   DC_SW_EXIT     $10

          .word     devend1
          jmp prg1
          .asc "video1",0

devend1   .word     devend2
          jmp prg2
          .asc "video2",0

#ifndef C64

devend2   .word     devend3
          jmp prg3
          .asc "video3",0

devend3   .word     devend
          jmp prg4
          .asc "video4",0

prg4      ldx #3
          .byt $2c

#else 

devend2   .word     devend
          jmp prg3
          .asc "video3",0

#endif

prg3      ldx #2
          bit freq
          bpl prg   ; 1 MHz dann ok
          lda #E_NODEV
          rts

prg2      ldx #1
          .byt $2c
prg1      ldx #0
prg       stx screen
          .)
          
screen    =sysmem
actscr    =sysmem+1
rspd      =sysmem+2
freq      =sysmem+3
bufscr    =sysmem+4
syscnt    =sysmem+5
xscreen   =sysmem+6
shfl      =sysmem+7
status    =sysmem+8
outstr    =sysmem+8+ANZSCR
getstr    =sysmem+8+2*ANZSCR
-sysmem   +=8+3*ANZSCR
zei       =syszp
zei2      =syszp+2
-syszp    +=4

          .(
          cmp #DC_RES
          bne p1
;.byt 2
          sty freq
          jsr inivid
          ldy screen
          lda #DC_SW_MOD      ; linein ein
          sta status,y
          lda #0
          sta outstr,y
          sta getstr,y
          tya
          bne re
          jsr inimem
          jsr inikbd
          jsr inibell
          lda #0
          sta syscnt
          jsr setact
&re       clc
          rts
          .)
          .(
p2x       jmp p2

#ifndef OLDROM
noirq	lda #E_NOIRQ
	.byt $2c
wasirq	lda #E_OK
	rts
#endif

&p1       pha
          lda status,x
          and #DC_SW_EXIT
          beq p1ok
          pla
          sec
          rts
p1ok      pla
          cmp #DC_IRQ
          bne p2x
          .(
#ifndef NOSYSPORT
#ifndef NOSINPORT
          bit SYSPORT
#ifdef OLDROM
          bpl re         ; kein TIRQ dann Ende
#else
	  bpl noirq
#endif
          lda SYSPORT
          sta SYSPORT
#endif
#endif          
          jsr bellirq

#ifndef C64		/* no interuptable video device */          
          lda syscnt
#ifdef OLDROM
          bmi re
#else
	  bmi wasirq
#endif
          lda SYSPORT
          and #1
          beq x1x	; CPU irq line active

          dec syscnt
          cli
          lda #0
          sta xscreen
x3        jsr setscr
          jsr x1
          inc xscreen
          lda xscreen
          cmp #2
          bcc x3
          bit freq
          bmi xe
          cmp #4
          bcc x3
xe        sei
          inc syscnt
#ifndef OLDROM
	lda #E_NOIRQ
#endif
          rts
#endif /* C64 */
          
x1x       lda screen
          sta xscreen
          jsr setscr
x1        .)
          .(
          ldy xscreen
          lda status,y
          and #DC_SW_EXIT
#ifdef OLDROM
          beq x1ok
          rts
#else
	  bne wasirq
#endif
x1ok      lda status,y
          and #DC_SW_RX
          beq p1c
          ldx outstr,y
          lda #SC_STAT
          jsr STRCMD
          cmp #E_NUL
          bne p1c
          jsr rxoff
p1c       .)
          .(
          ldx #0
          stx shfl
          ldy xscreen
          cpy actscr
          bne p1b
#ifdef C64
	  jsr crsrirq
#endif
          jsr irqkey
          stx shfl
          bcs p1b

#ifndef C64          
          cpy #$46       ; switchkey
#else
	  cpy #C64SWITCH ; switchkey (F1) Watch out ktab,y must be > 5
#endif

          bne p1c1
          dex
          bpl p1c2       ; without shift or ctrl
p1c1b     lda actscr     ; then switch screen
          clc
          adc #1
#ifndef C64
          and #3	 ; 4 screens
          bit freq
          bpl p1c1a
          and #1
#else
	  cmp #3	 ; C64 has three screens
	  bcc p1c1a
	  lda #0
#endif
p1c1a     sta actscr
          tay
          lda status,y
          and #DC_SW_EXIT
          bne p1c1b
          tya
          jsr setact
          jmp p1b

p1c2      dex
          bmi p1b        ; nur shift dann ende
          dex
          bpl p1c3       ; Ctrl only, then rxoff
          jsr rxoff
          jmp p1b

p1c3      dex
          bpl p1b        ; Ctrl und Shift dann Mod-switch
          ldy xscreen
          lda status,y
          eor #DC_SW_MOD
          sta status,y
          jmp p1b
         
	  ; no switch key 
p1c1      tax
          ldy xscreen
          lda status,y
          and #DC_SW_RX+DC_SW_TX+DC_SW_MOD
          cmp #DC_SW_RX+DC_SW_TX+DC_SW_MOD
          bne p1b1
          txa
          jsr lineout
          jmp p1b
          
p1b1      and #DC_SW_RX
          beq p1b
          txa
          ldx outstr,y
          jsr PUTC
p1b       .)
          .(
          ldy xscreen
          lda status,y
          and #DC_SW_TX
          beq p1d
          
p1e       ldy xscreen
          cpy actscr
          bne p1ea
          lda shfl       ;ctrl pressed, then stop actual screen output
          and #2
          bne p1d
/*
#ifdef C64		 ; test if crsr should be cleared
	  ldx getstr,y
	  jsr GETC
	  bcs p1f
	  pha
	  tya
	  pha
	  jsr crsrclr
	  pla
	  tay
	  pla
	  jmp gotchar
#endif
*/
p1ea     
	  ldx getstr,y
          jsr GETC
          bcs p1f
gotchar   jsr irqvid	; print char on screen
          jsr setlin
          ldy xscreen
          cpy actscr
          bne p1ea
          lda shfl
          and #1         ; shift pressed, then slow
          beq p1ea
          ;bne p1d
p1f       cmp #E_EOF
          bne p1d
          jsr txoff
p1d       .)
          .(
          lda actscr
          cmp xscreen
          bne p1g
          jsr setcrsr
p1g       
#ifndef OLDROM
#ifdef C64
	lda #E_NOIRQ
#else
	lda #E_OK
#endif
#endif
	  rts
          .)
          .)

p2        .(
          stx xscreen
          cmp #DC_TX_ON
          bne ox1q
          lda #DC_SW_TX
          bne ox1q1
ox1q      cmp #DC_RX_ON
          bne ox1
          lda #DC_SW_RX
ox1q1     ldy xscreen
          ora status,y
          sta status,y
          jmp ok
ox1       cmp #DC_TX_OFF
          bne ox2x
&txoff    ldy xscreen
          lda status,y
          and #DC_SW_TX
          beq devoff
          ldx getstr,y
          lda #SC_NUL
          jsr STRCMD
          lda status,y
          and #255-DC_SW_TX
          sta status,y
          jmp ok          
ox2x      cmp #DC_RX_OFF
          bne ox2
&rxoff    ldy xscreen
          lda status,y
          and #DC_SW_RX
          beq devoff
          ldx outstr,y
          lda #SC_EOF
          jsr STRCMD
          lda status,y
          and #255-DC_SW_RX
          sta status,y
          jmp ok
ox2       cmp #DC_EXIT
          bne ox2a
          jsr rxoff
          jsr txoff
          ldy xscreen
          lda #DC_SW_EXIT
          ora status,y
          sta status,y
          bne ok
ox2a      cmp #DC_ST
          bne o3
ok        lda #E_OK
          .byt $2c
devoff    lda #E_DOFF
          .byt $2c
devon     lda #E_DON
          .byt $2c
notimp    lda #E_NOTIMP
          cmp #1
          rts
o3        cmp #DC_PS
          bne o4
          ldx xscreen
          lda status,x
          and #DC_SW_RX
          bne devon
          tya
          sta outstr,x
          jmp ok
o4        cmp #DC_SPD
          bne o5
          sty rspd
          beq ok
o5        cmp #DC_GS
          bne notimp
          ldx xscreen
          lda status,x
          and #DC_SW_TX
          bne devon
          tya
          sta getstr,x
          jmp ok
          .)
 
/**************************************************************************/

inimem    .(
#ifndef NOMMU
#ifdef ROM
          ldx #$11
l1m       stx zei2
          stx MMU+1
          
          lda #16
          sta zei+1
          ldy #0
          sty zei
          ldx #>BLKSIZ
im1       lda (zei),y
          pha
          lda #$55
          sta (zei),y
          cmp (zei),y
          bne next
          asl
          sta (zei),y
          cmp (zei),y
          bne next
          lda #0
          sta (zei),y
          cmp (zei),y
          bne next
          pla
          sta (zei),y
          inc zei+1
          dex
          bne im1
    
          ldx zei2      
          jsr ENMEM
          pha
next      pla
          ldx zei2
          inx
#ifndef ROMTEST
          cpx #$20
#else
          cpx #$18
#endif
          bcc l1m
#endif /* ROM */
#endif /* NOMMU */
          clc
          rts
          .)

/**************************************************************************/

          
          .(
          

pnt       =sysmem
cnt       =sysmem+1
-sysmem   +=2

&inibell  
#ifndef C64
	  lda #%00100100
          sta VIA1+VIA_IER
          lda VIA1+VIA_ACR
          and #%11000011
          ora #%00010000
          sta VIA1+VIA_ACR
#endif
&trigbell 
#ifndef C64
	  lda #0
          sta pnt
          sta cnt
          lda #15
          sta VIA1+VIA_SR
#endif
&bellirq  
#ifndef C64
	  lda cnt
          bne endirq
          ldy pnt
          lda tcnt,y
          beq endbell
          sta cnt
          lda tfrq,y
          sta VIA1+VIA_T2CL
          lda #0
          sta VIA1+VIA_T2CH
          inc pnt
endirq    dec cnt
          rts
endbell   lda #0
          sta VIA1+VIA_SR
          rts
          
tcnt      .byt 2,2,2,2,2,2,2,0
tfrq      .byt 32,64,128,32,64,128,32

#else
	  rts
#endif
          .)
           
/**************************************************************************/
        
          .(  

spalte    =sysmem
zeile     =sysmem+1
wx        =sysmem+2
wy        =sysmem+3
wr        =sysmem+4
wh        =sysmem+5
hivid     =sysmem+6
iz        =sysmem+7
is        =sysmem+8
maxspal   =sysmem+10     ;maxspal-sysmem+2 muž gut zu rechnen sein
vtab      =sysmem+11     ;da bei setscr multipliziert
-sysmem   +=11+ANZSCR*(maxspal-spalte+2)
vzei      =syszp
-syszp    +=2

&inivid   .(   
          lda screen
          bne ix

#ifndef C64
          ldx #0
i1        bit freq
          bpl onemhz
          lda vdc8tab,x
          jmp nex
onemhz    lda vdc4tab,x
nex       stx vdc
          sta vdc+1
          inx
          cpx #16
          bcc i1

          lda #40
          bit freq
          bpl i2
          lda #80

#else /* C64 */
/* 
 * For the C64 we put the video screen memory to $e000-$ec00, i.e.
 * we have three possible screens only. We need $ec00-$efff for the 
 * stdlib. We use the std charom at $d000 in the RAM under I/O that 
 * the VIC doesn't see. The boot loader has to copy the charom there. 
 * Then we have all the memory below the I/O relatively free.
 */

	  lda #0
	  sta C64VIC+VIC_IRR		; no interrupts
	  sta C64VIC+VIC_SPRITE_EN	; sprites off
	  lda #27
	  sta C64VIC+VIC_CR1		; 25 lines
	  lda #8
	  sta C64VIC+VIC_CR2		; 40 cols
;	  lda #(($10 & %001110000)>>3)+(($20 & %00111100)<<2)
	  lda #%10000100
	  sta C64VIC+VIC_BASE		; character and screen base

	  lda C64CIA2+CIA_DDRA		; set upper two video address bits
	  ora #3			; via CIA2, PRA Bit 0 and 1
	  sta C64CIA2+CIA_DDRA		; set bits to output mode
	  lda C64CIA2+CIA_PRA
	  and #%11111100
	  sta C64CIA2+CIA_PRA		; change to uppermost page ($c000-)

	  lda #C64_TEXT_COL		; set the screen (text) color 
	  ldy #0
coloop	  sta $d800,y
	  sta $d900,y
	  sta $da00,y
	  sta $db00,y
	  iny
	  bne coloop

	  lda #C64_BGND_COL		; set the border and background color
	  sta C64VIC+VIC_BGND_COL0
	  lda #C64_BORDER_COL
	  sta C64VIC+VIC_EXT_COL

	  lda #40

#endif /* C64 */

i2        sta maxspal

#ifndef C64
          lda #$0c
          sta vdcctrl
#endif
          lda #1
          sta bufscr
ix        
          lda screen
          jsr setscr
          jsr iniscr
          lda #TC_FF     ; form feed
          jsr irqvid
          jsr setlin
          clc
          rts
          .)

&setlin   .(
          lda zeile
          sta iz
          lda spalte
          sta is
          rts
          .)
          
&lineout  .(
          cmp #TC_CR
          bne irqvid
#ifdef C64
	  jsr crsrclr
#endif
          jsr setmmu
          ldy zeile
          cpy iz
          bne liex
          ldy is
          cpy spalte
          bcc liex2
          beq liex2
liex      ldy wx
liex2     sty spalte
          sty zei2
          ldy wr
lie2      cpy spalte
          beq lie1
          dey
          lda (vzei),y
          cmp #" "
          beq lie2
          iny
lie1      sty zei2+1
lie3      ldy zei2
          cpy zei2+1
          bcs lie4
          lda (vzei),y
          ldy bufscr
          ldx outstr,y
          jsr PUTC
          jsr setmmu
          inc zei2
          bne lie3
lie4      lda #TC_CR
          ldy bufscr
          ldx outstr,y
          jmp PUTC
          .)
          
&irqvid   .(
#ifdef C64
	  pha
	  jsr crsrclr
	  pla
#endif
          jsr setmmu
          tay
          bmi x1
          cmp #32
          bcs outc
          jmp xout
x1        cmp #128+32
          bcs outc
          jmp xcode
          
outc      ldy spalte
          sta (vzei),y
          
&nextc    ldy spalte
          iny
          cpy wr
          bcc o1
          jsr nextz
          ldy wx
o1        sty spalte
          rts
          
&nextz    ldx zeile
          inx
          cpx wh
          bcc o2
          jsr scrollup
          ldx wh
          dex
o2        stx zeile

&setzei   stx zeile
          txa
          clc
          adc wy
          tax
          lda #0
          sta vzei
          sta vzei+1
          sta zei2+1
          lda maxspal
          sta zei2
szmul     txa
          beq sz1
          lsr
          tax
          bcc szm1
          lda zei2:clc:adc vzei:sta vzei
          lda zei2+1:adc vzei+1:sta vzei+1
szm1      asl zei2:rol zei2+1
          jmp szmul
sz1       clc
          lda hivid
          adc vzei+1
          sta vzei+1
          ldx zeile
          rts
        
scrollup  ldx #0
          jsr setzei

su2       inx
          cpx wh
          bcs su3
          lda vzei
          clc
          adc maxspal
          sta zei2
          lda vzei+1
          adc #0
          sta zei2+1
          ldy wx
su1       lda (zei2),y
          sta (vzei),y
          iny
          cpy wr
          bcc su1
          
          lda zei2
          sta vzei
          lda zei2+1
          sta vzei+1
          jmp su2     

su3       ldy wx
          lda #" "
su4       sta (vzei),y
          iny
          cpy wr
          bcc su4
          
&clrli    ldx #<-1
          stx iz
          rts
          .)

iniscr    .(
          lda #0
          sta wx
          sta wy
          sta spalte
          lda maxspal
          sta wr
          lda maxzeil
          sta wh
          lda bufscr
          asl
          asl
          bit freq
          bpl i1
          asl
i1        clc
          adc #VIDPAGE<<4
          sta hivid
          ldx #0
          jmp setzei
          .)

&setscr   .(
          cmp bufscr
          beq setmmu
          pha
          lda bufscr
          asl
          asl
          sta zei2+1          
          asl
          clc
          adc zei2+1
          tax
          ldy #0
s1        lda spalte,y
          sta vtab,x
          inx
          iny
          cpy #10
          bcc s1
          lda vzei
          sta vtab,x
          lda vzei+1
          sta vtab+1,x
          pla
          sta bufscr
          asl
          asl
          sta zei2+1
          asl
          clc
          adc zei2+1
          tax
          ldy #0
s2        lda vtab,x
          sta spalte,y
          inx
          iny
          cpy #10
          bcc s2
          lda vtab,x
          sta vzei
          lda vtab+1,x
          sta vzei+1
&setmmu   
#ifndef NOMMU
	  ldy #$10
          sty MMU+VIDPAGE
#endif
          rts
          .)

&setact   .(
          sta actscr
#ifndef C64
          asl
          asl
          bit freq
          bpl s1
          asl
s1        and #%00001111
          ldx #12
          stx vdc
          sta vdc+1
#else
	  asl 
	  asl
	  asl
	  asl
	  ora #%10000100
	  sta C64VIC+VIC_BASE
#endif
          rts
          .)

#ifndef C64
&setcrsr  .(
          ldx #15
          lda spalte
          clc
          adc vzei
          stx vdc
          sta vdc+1
          lda vzei+1
          adc #0
          dex
          stx vdc
          and #%00001111
          sta vdc+1
	  rts
	  .)
#else

	  .(
crsrfl	=sysmem
crsrcnt	=sysmem+1
-sysmem +=2

&&setcrsr			; enable cursor
	  bit crsrfl
	  bmi ret		; already enabled
	  lda #0		; inverse cursor next crsrirq
	  sta crsrcnt
	  lda #128		; enabled, but not invers
	  sta crsrfl
ret       rts

&&crsrclr			; disable cursor
	  bit crsrfl
	  bpl ret		; not enabled
	  lda actscr
	  cmp xscreen
	  bne ret		; not on visible screen - ignore

	  bit crsrfl
	  bvc clr		; not inversed
;	  ldx zeile
;	  jsr setzei
	  ldy spalte
	  lda (vzei),y
	  eor #$80
	  sta (vzei),y
clr	  lda #0
	  sta crsrfl
	  rts

&&crsrirq			; blink cursor
	  bit crsrfl
	  bpl ret		; not enabled
	  dec crsrcnt
	  bpl ret		; not this time
	  lda #10
	  sta crsrcnt
;	  ldx zeile
;	  jsr setzei
php:sei
	  ldy spalte
	  lda (vzei),y
	  eor #$80
	  sta (vzei),y
	  lda crsrfl
	  eor #$40
	  sta crsrfl
plp
	  rts
          .)
#endif
                    
xout      .(
          cmp #TC_CR
          bne x1
          lda wx
          sta spalte
          rts
x1        cmp #TC_LF
          bne x2
          jmp nextz
x2        cmp #TC_BEL
          bne x3a
          jmp trigbell
x3a       .(
          cmp #TC_FF
          bne x3
&&toclr   ldx #0
c2b       jsr setzei
          ldy wx
          lda #" "
c2a       sta (vzei),y
          iny
          cpy wr
          bcc c2a
          inx
          cpx wh
          bcc c2b
          jsr clrli
&&tohome  lda wx
          sta spalte
          ldx #0
          jmp setzei
          .)
x3        .(
          cmp #TC_BS
          bne x4
          ldy spalte
          cpy wx
          beq c7
          dec spalte
bslp      lda (vzei),y
          dey
          sta (vzei),y
          iny
          iny
          cpy wr
          bcc bslp
          dey
          lda #" "
          sta (vzei),y
&x4 c7    rts
          .)
          .)

xcode     .(
          and #$7f
          cmp #maxcode
          bcs xe
          asl
          tax
          lda xadr+1,x
          pha
          lda xadr,x
          pha
xe        rts

maxcode   =13
xadr      .word tocl-1,nextc-1,tocu-1,nextz-1
          .word tohome-1,toclr-1,todel-1,toins-1
          .word towlo-1,towru-1,towcl-1
          .word toeol-1,toclrl-1

todel     .(
          ldy spalte
td0       iny
          cpy wr
          bcs td1
          lda (vzei),y
          dey
          sta (vzei),y
          iny
          bne td0
td1       dey
          lda #" "
          sta (vzei),y
          rts
          .)

toins     .(
          ldy wr
          dey
          lda (vzei),y
          cmp #" "
          bne tie
          cpy spalte
          beq tie
ti1       dey
          lda (vzei),y
          iny
          sta (vzei),y
          dey
          cpy spalte
          bne ti1
          lda #" "
          sta (vzei),y
tie       rts
          .)

toeol     .(
          ldy wr
l         dey
          bmi e
          lda (vzei),y
          cmp #" "
          beq l
e         iny 
          cpy wr
          bcs x
          sty spalte
x         rts
          .)

toclrl    .(
          lda #" "
          ldy spalte
c         sta (vzei),y
          iny
          cpy wr
          bcc c
          rts
          .)
          
tocl      .(
          ldy spalte
          cpy wx
          beq c4
          dec spalte
          rts
c4        ldy wr
          dey
          sty spalte
&tocu     ldx zeile
          beq c5
          dex
c5        jsr setzei
          rts
          .)
          
towlo     .(
          lda spalte
          sta wx
          lda wh
          sec
          sbc zeile
          sta wh
          lda zeile
          clc
          adc wy
          sta wy
          lda #0
          sta zeile
          jmp clrli
          .)
          
towru     .(
          ldy spalte
          iny
          sty wr
          ldx zeile
          inx
          stx wh
          jmp clrli
          .)
          
towcl     .(
          lda zeile
          clc
          adc wy
          sta zeile
          lda #0
          sta wx
          sta wy
          lda maxspal
          sta wr
          lda maxzeil
          sta wh
          jmp clrli
          .)
          .)

#ifndef C64                    
vdc8tab   .byt $7e,$50,$64,6,$29,0,$19,$21,0,7,$60,7,0,0,0,0

vdc4tab   .byt $3f,$28,$32,3,$29,0,$19,$21,0,7,$60,7,0,0,0,0
#endif          
          .)
          
/**************************************************************************/

          .(

cnt       =zei2
rcnt      =sysmem
shiftfl   =sysmem+1
prev      =sysmem+2
-sysmem   +=3
tzei      =syszp
-syszp    +=2

&inikbd   .(
#ifndef C64
          lda PIO+PIA_CRA
          and #255-4
          sta PIO+PIA_CRA
          ldx #15
          stx PIO+PIA_PA
          ora #4
          sta PIO+PIA_CRA
          lda PIO+PIA_CRB
          and #255-4
          sta PIO+PIA_CRB
          ldx #0
          stx PIO+PIA_PB
          ora #4
          sta PIO+PIA_CRB 
	  lda #4
#else /* C64 */
	  lda #0
	  sta C64CIA1+CIA_DDRB
	  lda #$ff
	  sta C64CIA1+CIA_DDRA
	  sta C64CIA1+CIA_PRA
          lda #4
#endif /* C64 */
          sta rcnt
          sta rspd
          rts
          .)

#ifndef C64

&irqkey   .(
          lda PIO
          and #$f0
          ora #8
          sta PIO
ix1       
	  lda PIO+2
          cmp PIO+2
          bne ix1   
          ldy #0
          and #%00100001
          cmp #%00100001
          beq nshift
          iny
nshift    inc PIO
x0        lda PIO+2
          cmp PIO+2
          bne x0
          and #%00000001
          bne nctrl
          iny
          iny
nctrl     sty shiftfl

          tya
          asl
          tax
          lda ttab+1,x
          tay
          lda ttab,x
          sta tzei
          sty tzei+1   
          ldy #0
          ldx #0
lx1       stx cnt
          lda PIO
          and #$f0
          ora cnt
          sta PIO
          ldx #8
l2        lda PIO+2
          cmp PIO+2
          bne l2
l3        asl
          bcs lc
          pha
          lda (tzei),y
          bne le
          pla
lc        iny
          dex
          bne l3
          ldx cnt
          inx
          cpx #10
          bcc lx1

          ldy #<-1
          sty prev
nokey     
/* TODO: check keyboard code - C64 works different */
;	  ldx #KEY_START
;	  stx rcnt
	  ldx shiftfl
          sec
          rts

le        tax
          pla
          txa
          cpy prev
          sty prev
          bne lx

          dec rcnt
          bne nokey
          ldx rspd
          .byt $2c

lx        ldx #KEY_START
          stx rcnt

          ldx shiftfl
          clc
          rts
          .)
          
ttab      .word ktab,stab,ctab,sctab

ktab      .asc TC_CRGT,TC_HOME,"~(&%#!",TC_BS,TC_CDWN,"^@)\'$":.byt $22
          .asc "97^^outeq/8^@piyrw"
          .asc "64^@ljgda*5^@:khfs"
          .asc "31",TC_CR,";mbcz+2^@?,nvx"
          .asc "-0^@>^@]@^@=.^@@< [^@"

stab      .asc TC_CLFT,TC_CLR,"|(&%#!",TC_INS,TC_CUP,"^@)\'$":.byt $22
          .asc "97^^OUTEQ/8^@PIYRW"
          .asc "64^@LJGDA*5^@:KHFS"
          .asc "31",TC_LF,";MBCZ+2^@?,NVX"
          .asc "-0^@>^@}@^@=.^@",TC_ESC,"< {^@"

ctab      .asc TC_WRU,TC_WCLS,"~(&%#!",TC_DEL,TC_WLO,"^@)\'$",$22
          .asc "97^^^o^u^t^e^q/8^@^p^i^y^r^w"
          .asc "64^@^l^j^g^d^a*5^@:^k^h^f^s"
          .asc "31",TC_EOL,";^m^b^c^z+2^@?,^n^v^x"
          .asc "-0^@>^@]@^@=.^@",TC_CLL,"< [^@"

sctab     .asc "^@^@|(&%#!^H^@^@)\'$":.byt $22
          .asc "97^^O^U^T^E^Q/8^@^P^I^Y^R^W"
          .asc "64^@^L^J^G^D^A*5^@:^K^H^F^S"
          .asc "31^m;^M^B^C^Z+2^@?,^N^V^X"
          .asc "-0^@>^@}@^@=.^@@< {^@"

#else /* C64 */

&irqkey	  .(
	  lda #0
	  sta shiftfl
	  ldy #$ff
	  sty cnt

	  sta C64CIA1+CIA_PRA
l0	  lda C64CIA1+CIA_PRB
	  cmp C64CIA1+CIA_PRB
	  bne l0
	  cmp #$ff
	  beq nokey

	  lda #%11111110
	  sta C64CIA1+CIA_PRA

l1	  lda C64CIA1+CIA_PRB
	  cmp C64CIA1+CIA_PRB
	  bne l1
	  ldy #8

nextbit	  lsr 
	  bcs nobit
	  pha
	  lda ktab,x
	  cmp #5
	  bcs realkey
	  cmp #3
	  beq realkey
	  ora shiftfl
	  sta shiftfl
	  bpl l3
realkey	  stx cnt
l3	  pla
nobit	  inx
	  cpx #65
	  bcs endkey
	  dey
	  bne nextbit		; test next bit
	  sec
	  rol C64CIA1+CIA_PRA
	  bne l1		; always

endkey	  lda cnt
	  cmp #$ff
	  beq nokey
	  cmp prev
          bne lx

          dec rcnt
          bne nkret
          ldx rspd
          .byt $2c

lx        ldx #KEY_START
          stx rcnt

	  sta prev

	  tay
	  lda shiftfl
	  and #3
	  asl
	  tax
	  lda ttab,x
	  sta tzei
	  lda ttab+1,x
	  sta tzei+1

	  lda (tzei),y
          ldx shiftfl
          clc
          rts
 
nokey     ldx #<-1
	  stx prev
	  ldx #KEY_START
	  stx rcnt
nkret	  ldx shiftfl
	  sec
	  rts
	  .)

ttab	  .word ktab, stab, ctab, sctab

ktab	  .byt TC_BS, TC_CR, TC_CRGT, 0, 6 /* F1 */, 0, 0, TC_CDWN
	  .byt "3wa4zse", 1 /* shift */
	  .byt "5rd6cftx7yg8bhuv"
	  .byt "9ij0mkon+pl-.:@,"
	  .byt "\*;", TC_HOME, 1 /* shift */, "=^^/"
	  .byt "1_", 2 /* control */, "2 ", 6 /* C= */, "Q", 3 /* stop */

stab	  .byt TC_INS, TC_LF, TC_CLFT, 0, 0, 0, 0, TC_CUP
	  .byt "#WA$ZSE", 1 /* shift */
	  .byt "%RD&CFTX'YG(BHUV"
	  .byt ")IJ0MKON+PL->[@<"
	  .byt "\*]", TC_CLR, 1 /* shift */, "=^^?"
	  .byt "!", TC_ESC, 2 /* control */, 34," ", 6 /* C= */, "Q", 3 /*stop*/

ctab	  .byt TC_DEL, TC_EOL, TC_WRU, 0, 0, 0, 0, TC_WLO
	  .byt "3^w^a4^z^s^e", 1 /* shift */
	  .byt "5^r^d6^c^f^t^x7^y^g8^b^h^u^v"
	  .byt "9^i^j0^m^k^o^n+^p^l-.:@,"
	  .byt "\*;", TC_WCLS, 1 /* shift */, "=^^/"
	  .byt "1", TC_CLL, 2 /* control */, "2 ", 6 /* C= */, "Q", 3 /*stop*/

sctab	  .byt TC_INS, TC_LF, TC_CLFT, 0, 0, 0, 0, TC_CUP
	  .byt "3^W^A4^Z^S^E", 1 /* shift */
	  .byt "5^R^D6^C^F^T^X7^Y^G8^B^H^U^V"
	  .byt "9^I^J0^M^K^O^N+^P^L-.:@,"
	  .byt "\*;", TC_CLR, 1 /* shift */, "=^^/"
	  .byt "1_", 2 /* control */, "2 ", 6 /* C= */, "Q", 3 /* stop */

#endif /* C64 */

          .)

devend    .)
 
