/****************************************************************************
   
    OS/A65 Version 1.3.3
    Multitasking Operating System for 6502 Computers

    Copyright (C) 1989-1996 Andre Fachat 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

****************************************************************************/


#ifndef NOMMU
#define   MBLOCKS   256
#else
#define   MBLOCKS   16
#endif

          .(
#ifdef NOMMU
MEMVAL    =sysmem
MEMFRE    =sysmem+MBLOCKS/8
div       =sysmem+MBLOCKS/4
-sysmem   +=1+MBLOCKS/4
#else
MEMVAL    =syszp
MEMFRE    =syszp+MBLOCKS/8
div       =syszp+MBLOCKS/4
-syszp    +=1+MBLOCKS/4
#endif

+inimem   ldy #MBLOCKS/8
          lda #0
l1        sta MEMVAL,y
          sta MEMFRE,y
          dey
          bpl l1
          clc
          rts

+getmem   jsr memsys
          ldx #0
l2        lda MEMVAL,x
          and MEMFRE,x
          beq gnext
          ldy #7
l3        asl
          bcs found
          dey
          bpl l3
found     lda POT2,y
          eor #$FF
          and MEMFRE,x
          sta MEMFRE,x
          txa
          asl
          asl
          asl
          sta div
          tya
          clc
          adc div
          tax
          lda #E_OK
          jmp gne
gnext     inx
          cpx #MBLOCKS/8
          bcc l2
notfre    lda #E_NOMEM
          .byt $2c
gne       lda #E_OK
          jsr memtask
          cmp #1
          rts

+fremem   jsr memsys
#ifdef NOMMU
          cpx #MBLOCKS
          bcs notfre
#endif
          jsr gsetxy
          ora MEMFRE,x
          and MEMVAL,x
          sta MEMFRE,x
          lda #E_OK
          jsr memtask
          clc
          rts

+enmem    jsr memsys
#ifdef NOMMU
          cpx #MBLOCKS
          bcs notfre
#endif
          jsr gsetxy
          ora MEMVAL,x
          sta MEMVAL,x
          lda POT2,y
          ora MEMFRE,x
          sta MEMFRE,x
          lda #E_OK
          jsr memtask
          clc
          rts

+getblk   jsr memsys
#ifdef NOMMU
          cpx #MBLOCKS
          bcs notfre
#endif
          jsr gsetxy
          and MEMFRE,x
          and MEMVAL,x
          beq notfre
          lda POT2,y
          eor #$ff
          and MEMFRE,x
          sta MEMFRE,x
          lda #E_OK
          jsr memtask
          clc
          rts
          .)

